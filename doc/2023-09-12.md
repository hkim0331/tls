# 2023-09-12

## 字下げ（インデント）

LISP/Schemeでは複数の連続する空白文字はただ一つの空白文字と同じ。

インデントはコードの見やすさのためだけにある。インデントするときは2文字落とそう。

LISP/Scheme ではインデントなくてもコードの意味は変わらないが、
Python はインデントが文法の一部になっている。

```scheme
(define name
  (lambda (arg)
    (body)))
```

- 関数の名前は name
- 引数はただ一つ、arg
- 関数本体が body

cond は多分岐。分岐先をインデント合わせて書くと見やすい。

```scheme
(define phonic
  (lambda (n)
    (cond
      ((= n 0) "zero")
      ((= n 1) "one")
      ((= n 2) "two")
      (else "other"))))
```

上はこう書いてもコードの意味は変わらない。

```scheme
(define phonic
  (lambda (n) (cond ((= n 0) "zero") ((= n 1) "one") ((= n 2) "two") (else "other"))))
```

## ターミナルから racket
VScode を起動せず、ターミナルから racket でも起動できる。

```shell
wsl$ racket
Welcome to Racket v8.6 [cs].
> (+ 1 2)
3
```

## 補助関数を使った再帰

ごく自然な再帰で。補助関数なし。

```scheme
(define length
  (lambda (lst)
    (cond
      ((null? lst) 0)
      (else (+ 1 (length (rest lst)))))))
```
補助関数 length2-aux を導入、同時に補助変数 n も導入する。
補助変数は戻り値予定のもの。

```scheme
(define length2-aux
  (lambda (lst n)
    (cond
      ((null? lst) n)
      (else (length2-aux (rest lst) (+ 1 n))))))

(define length2
  (lambda (lst)
    (length2-aux lst 0)))
```

リスト lst から最初に見つかるアトム a を除いたリストを返す。

```scheme
(define rember
  (lambda (a lst)
    (cond
      ((null? lst) '())
      ((eq? (first lst) a) (rest lst))
      (else (cons (first lst)
                  (rember a  (rest lst)))))))

; (rember '3 '(1 2 3 4 5))
```

補助関数 rember2-aux の導入。補助変数 ret に戻りリストを作る。
戻りリストの要素順がちょっと気になるが。。。

```scheme
(define rember2-aux
  (lambda (a lst ret)
    (cond
      ((null? lst) ret)
      ((eq? (first lst) a) (append (rest lst) ret))
      (else (rember2-aux a (rest lst) (cons (first lst) ret))))))

(define rember2
  (lambda (a lst)
    (rember2-aux a lst '())))

; (rember2 1 '(1 2 3 4 5))
; (rember2 3 '(1 2 3 4 5))
; (rember2 5 '(1 2 3 4 5))
```
